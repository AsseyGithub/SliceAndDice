plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.0' apply false
//    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
//    apply plugin: "dev.architectury.loom"

    //loom {
    //    silentMojangMappingsLicense()
    //}

    //dependencies {
    //    minecraft "com.mojang:minecraft:${rootProject.mc_version}"
    //    mappings loom.layered() {
    //        officialMojangMappings()
    //        // parchment("org.parchmentmc.data:parchment-1.17.1:2021.10.10")
    //    }
    //}

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()
        
    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : mc_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {
        mavenCentral()

        maven {
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }

        maven {
            url = "https://thedarkcolour.github.io/KotlinForForge/"
            content {
                includeGroup "thedarkcolour"
            }
        }
        
        maven {
            url = 'https://www.cursemaven.com'
            content {
                includeGroup "curse.maven"
            }
        }

        maven {
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }

        maven {
            url = "https://dvs1.progwml6.com/files/maven/"
            content {
                includeGroup "mezz.jei"
            }
        }

        maven {
            url = "https://maven.architectury.dev/"
            content {
                includeGroup "dev.architectury"
            }
        }

        maven {
            url = "https://maven.saps.dev/minecraft"
            content {
                includeGroup "dev.latvian.mods"
            }
        }

        maven {
            url = "https://maven.theillusivec4.top/"
            content {
                includeGroup "top.theillusivec4.curios"
            }
        }

        // Forge
        maven {
            url = "https://maven.tterrag.com/"
            content {
                includeGroup "com.simibubi.create"
                includeGroup "com.jozufozu.flywheel"
                includeGroup "com.tterrag.registrate"
                includeGroup "com.tterrag.registrate_fabric"
            }
        }
        maven {
            url = "https://maven.jaackson.me"
            content {
                includeGroup "com.teamabnormals"
            }
        }

        // Fabric
        maven {
            url = "https://mvn.devos.one/snapshots/"
            content {
                includeGroup "com.simibubi.create"
                includeGroup "io.github.fabricators_of_create.Porting-Lib"
                includeGroup "io.github.tropheusj"
                includeGroup "com.tterrag.registrate_fabric"
            }
        }

        maven {
            url = "https://maven.jamieswhiteshirt.com/libs-release"
            content {
                includeGroup "com.jamieswhiteshirt"
            }
        }

        maven {
            url = "https://jitpack.io"
            content {
                includeGroup "com.github.LlamaLad7"
                includeGroup "com.github.Chocohead"
            }
        }

        maven {
            url = "https://maven.blamejared.com"
            content {
                includeGroup "com.faux.ingredientextension"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        source(project(":common").sourceSets.main.allSource)
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.named("compileKotlin") {
        source(project(":common").sourceSets.main.allSource)
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}
